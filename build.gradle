buildscript {
  ext {
    isCi = Boolean.parseBoolean(System.getProperty('CI', 'false'))

    deps = [
        'kotlin': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.41",
        'okhttp': 'com.squareup.okhttp3:okhttp:3.8.0',
        'guavaAndroid': 'com.google.guava:guava:23.0-android',
        'truth': 'com.google.truth:truth:0.34',
        'junit': 'junit:junit:4.12'
    ]

    release = [
        'groupId': 'com.caseykulm.oauthheader',
        'version': '0.4.0',
        'description': 'OkHttp OAuth 1.0 3-legged Helper',
        'githubRepo': 'https://github.com/caseykulm/oauthheader',
        'secrets': [
            // Add secrets to System Environment Variables or Gradle Properties
            'bintray': [
                'user':
                    System.env.BINTRAY_USER ?:
                        (project.hasProperty('BINTRAY_USER') ? project.property('BINTRAY_USER') : ''),
                'key':
                    System.env.BINTRAY_API_KEY ?:
                        (project.hasProperty('BINTRAY_API_KEY') ? project.property('BINTRAY_API_KEY') : ''),
                'gpgPassphrase':
                    System.env.BINTRAY_GPG_PASSWORD ?:
                        (project.hasProperty('BINTRAY_GPG_PASSWORD') ? project.property('BINTRAY_GPG_PASSWORD') : ''),
            ],
            'sonatype': [
                'user':
                    System.env.SONATYPE_USER ?:
                        (project.hasProperty('SONATYPE_USER') ? project.property('SONATYPE_USER') : ''),
                'password':
                    System.env.SONATYPE_PASSWORD ?:
                        (project.hasProperty('SONATYPE_PASSWORD') ? project.property('SONATYPE_PASSWORD') : ''),
            ]
        ]
    ]
  }

  repositories {
    jcenter()
    mavenCentral()
  }

  dependencies {
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.41"
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.15"
  }
}

subprojects { project ->
  apply plugin: 'kotlin'
  apply plugin: 'org.jetbrains.dokka'

  repositories {
    mavenCentral()
  }

  compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
  }
  compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
  }

  dokka {
    moduleName = "${project.name}"
    outputFormat = 'gfm'
    outputDirectory = "${project.rootProject.rootDir}/kotlindocs"

    jdkVersion = 8 // Used for linking to JDK
  }
}

allprojects { apply from: "$rootProject.projectDir/gradle/maven-publish.gradle" }
